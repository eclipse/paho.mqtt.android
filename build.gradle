// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
	    classpath 'com.android.tools.build:gradle:2.2.3'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext{

    compileSdkVersion = 24
    buildToolsVersion = '23.0.3'


    group = 'org.eclipse.paho'

    sampleArchivesBaseName = 'org.eclipse.paho.android.sample'
    sampleVersion = '1.1.2-SNAPSHOT'

    serviceArchivesBaseName = 'org.eclipse.paho.android.service'
    serviceVersion = '1.1.2-SNAPSHOT'

    clientVersion = '1.1.1-SNAPSHOT'

    mavenUrl = "https://repo.eclipse.org/content/repositories/paho-snapshots/"

    supportLibVersion = '24.2.1'

}

def getSdkDirFromLocalProperties() {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
        properties.load(instr)
    }
    properties.getProperty('sdk.dir')
}

def androidHome = System.env['ANDROID_HOME']

def androidHomeIsDefined = androidHome?.trim()

def sdkDir = androidHomeIsDefined ? androidHome : getSdkDirFromLocalProperties()

println "SDK dir: $sdkDir"


allprojects {

    repositories {
        jcenter()
        maven {
            url "file://$sdkDir/extras/android/m2repository/"
        }
    }

    version = rootProject.ext.serviceVersion
    group = rootProject.ext.group
}
